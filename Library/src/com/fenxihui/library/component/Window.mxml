<?xml version="1.0" encoding="utf-8"?>
<mx:Window xmlns:fx="http://ns.adobe.com/mxml/2009"
		   xmlns:s="library://ns.adobe.com/flex/spark"
		   xmlns:mx="library://ns.adobe.com/flex/mx"
		   alwaysInFront="false" autoLayout="true" creationComplete="initWindow()"
		   horizontalAlign="left" showStatusBar="false" styleName="window" systemChrome="none"
		   title="全能股市操盘系统" transparent="true" titleIcon="@Embed(source='assets/icon_title.png')">
	<fx:Metadata>
		[Style(name = "topLeftRadius" ,defaultValue='0', type = "uint", format = "Length", inherit = "no" )]
		[Style(name = "topRightRadius" ,defaultValue='0', type = "uint", format = "Length", inherit = "no" )]
		[Style(name = "bottomLeftRadius" ,defaultValue='0', type = "uint", format = "Length", inherit = "no" )]
		[Style(name = "bottomRightRadius" ,defaultValue='0',  type = "uint", format = "Length", inherit = "no" )]
		[Style(name="cornerRadius", type="Number", format="Length", inherit="no", theme="halo, spark")]
		[Style(name = "borderThickness" ,defaultValue='1', type = "uint", format = "Length", inherit = "no" )]
		[Style(name = "borderColor" ,defaultValue='0xb60002', type = "uint", format = "Length", inherit = "no" )]
		[Style(name = "backgroundVisible" ,defaultValue='true', type = "Boolean" , inherit = "no" )]
		[Style(name = "backgroundColor" ,defaultValue="0xefefef", type = "uint" , format = "Color" , inherit = "no" )]
		[Style(name = "backgroundAlpha" ,defaultValue="1.0" , type = "Number" , format = "Length" , inherit = "no" )]
		[Style(name = "backgroundImage" , type = "Class" , format = "EmbeddedFile" , inherit = "no" )]
		[Style(name = "backgroundPositionLeft",defaultValue='0' , type = "int" , inherit = "no" )]
		[Style(name = "backgroundPositionTop",defaultValue='0' , type = "int" , inherit = "no" )]
		[Style(name = "backgroundRepeat" ,defaultValue='false', type = "Boolean" , inherit = "no" )]
	</fx:Metadata>
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		@namespace mx "library://ns.adobe.com/flex/mx";
		.window{
			titleTextStyleName:"title";
			cornerRadius:6px;
			dropShadowVisible:true;
			dropShadowEnabled:true;
			shadowDirection:right;
			shadowDistance:10px;
			dropShadowColor:#ff0000;
			borderThickness:1;
			borderColor:#b60002;
			backgroundColor:#efefef;
			backgroundAlpha:1;
			titleBarColors:#b60002,#960002;
			minimizeButtonSkin:ClassReference("skins.button.MinimizeButtonSkin");
			maximizeButtonSkin:ClassReference("skins.button.MaximizeButtonSkin");
			restoreButtonSkin:ClassReference("skins.button.RestoreButtonSkin");
			closeButtonSkin:ClassReference("skins.button.CloseButtonSkin");
		}
		.title{
			fontSize:14px;
			textIndent:5px;
			color:white;
			fontWeight:bold;
			chromeColor:white;
		}
	</fx:Style>
	<fx:Script>
		<![CDATA[
			import mx.controls.ToolTip;
			import mx.core.Container;
			import mx.core.UIComponent;
			import mx.core.windowClasses.TitleBar;
			import mx.events.ResizeEvent;
			import mx.events.ToolTipEvent;
			import mx.utils.GraphicsUtil;
			
			import skins.TitleBarSkin;
			use namespace mx_internal;
			
			private var _closeButtonVisible:Boolean=true;
			private var _maxButtonVisible:Boolean=true;
			private var _minButtonVisible:Boolean=true;

			public function get minButtonVisible():Boolean
			{
				return _minButtonVisible;
			}

			public function set minButtonVisible(value:Boolean):void
			{
				_minButtonVisible = value;
				if(!sysTitleBar || !sysTitleBar.minimizeButton){
					return;
				}
				if(_minButtonVisible==false){
					//sysTitleBar.minimizeButton.visible=sysTitleBar.minimizeButton.includeInLayout=false;
					if(sysTitleBar==sysTitleBar.minimizeButton.parent){
						sysTitleBar.removeChild(sysTitleBar.minimizeButton);
					}
				}else{
					//sysTitleBar.minimizeButton.visible=sysTitleBar.minimizeButton.includeInLayout=true;
					sysTitleBar.addChild(sysTitleBar.minimizeButton);
					sysTitleBar.minimizeButton.buttonMode=true;
					sysTitleBar.minimizeButton.useHandCursor=true;
				}
			}

			public function get maxButtonVisible():Boolean
			{
				return _maxButtonVisible;
			}

			public function set maxButtonVisible(value:Boolean):void
			{
				_maxButtonVisible = value;
				if(!sysTitleBar || !sysTitleBar.maximizeButton){
					return;
				}
				if(_maxButtonVisible==false){
					sysTitleBar.doubleClickEnabled=false;
					//sysTitleBar.maximizeButton.visible=sysTitleBar.maximizeButton.includeInLayout=false;
					if(sysTitleBar==sysTitleBar.maximizeButton.parent){
						sysTitleBar.removeChild(sysTitleBar.maximizeButton);
					}
				}else{
					sysTitleBar.doubleClickEnabled=true;
					//sysTitleBar.maximizeButton.visible=sysTitleBar.maximizeButton.includeInLayout=true;
					sysTitleBar.addChild(sysTitleBar.maximizeButton);
					sysTitleBar.maximizeButton.buttonMode=true;
					sysTitleBar.maximizeButton.useHandCursor=true;
				}
			}

			public function get closeButtonVisible():Boolean
			{
				return _closeButtonVisible;
			}

			public function set closeButtonVisible(value:Boolean):void
			{
				_closeButtonVisible = value;
				if(!sysTitleBar || !sysTitleBar.closeButton){
					return;
				}
				if(_closeButtonVisible==false){
					//sysTitleBar.closeButton.visible=sysTitleBar.closeButton.includeInLayout=false;
					if(sysTitleBar==sysTitleBar.closeButton.parent){
						sysTitleBar.removeChild(sysTitleBar.closeButton);
					}
				}else{
					//sysTitleBar.closeButton.visible=sysTitleBar.closeButton.includeInLayout=true;
					sysTitleBar.addChild(sysTitleBar.closeButton);
					sysTitleBar.closeButton.buttonMode=true;
					sysTitleBar.closeButton.useHandCursor=true;
				}
			}

			protected function get sysTitleBar():TitleBar{
				return titleBar as TitleBar;
			}
			
			override public function get closed():Boolean
			{
				return (!nativeWindow || nativeWindow.closed);
			}
			
			private var backgroundAlpha:Number;

			protected function toolTipCreateHandler(event:ToolTipEvent):void
			{
				var tip:ToolTip=new ToolTip;
				tip.setStyle('fontSize',14);
				event.toolTip=tip;
			}

			override protected function createChildren():void{
				backgroundAlpha=getStyle('backgroundAlpha');
				super.createChildren();
				
				if(!sysTitleBar){
					if(getStyle('borderVisible')){
						var border:Number=getStyle('borderThickness');
						setStyle('paddingLeft',border);
						setStyle('paddingRight',border);
						setStyle('paddingTop',border);
						setStyle('paddingBottom',border);
					}
					return;
				}
				sysTitleBar.minimizeButton.toolTip='最小化窗口';
				sysTitleBar.minimizeButton.addEventListener(ToolTipEvent.TOOL_TIP_CREATE,toolTipCreateHandler);
				sysTitleBar.maximizeButton.toolTip='最大化窗口';
				nativeWindow.addEventListener(NativeWindowDisplayStateEvent.DISPLAY_STATE_CHANGING,function(e:NativeWindowDisplayStateEvent):void{
					if(sysTitleBar && sysTitleBar.maximizeButton){
						sysTitleBar.maximizeButton.toolTip=(e.afterDisplayState == NativeWindowDisplayState.MAXIMIZED?'还原窗口':'最大化窗口');
					}
				});
				sysTitleBar.maximizeButton.addEventListener(ToolTipEvent.TOOL_TIP_CREATE,toolTipCreateHandler);
				sysTitleBar.closeButton.toolTip='关闭窗口';
				sysTitleBar.closeButton.addEventListener(ToolTipEvent.TOOL_TIP_CREATE,toolTipCreateHandler);

				minButtonVisible=_minButtonVisible;
				maxButtonVisible=_maxButtonVisible;
				closeButtonVisible=_closeButtonVisible;
			}
			private function initWindow():void{
				if(titleBar && titleBar.visible){
					height+=titleBar.height;
				}
				if(statusBar && statusBar.visible)
					height+=statusBar.height;
			}
			
			public function center():void{
				nativeWindow.bounds=new Rectangle((Screen.mainScreen.bounds.width / 2 - (width/2)), (Screen.mainScreen.bounds.height / 2 - (height/2)), width, height);
			}
			
			protected override function updateDisplayList(w:Number,h:Number):void{
				super.updateDisplayList(w,h);
				
				graphics.clear();
				
				if(getStyle('backgroundVisible')!=false){
					graphics.beginFill(getStyle('backgroundColor'),backgroundAlpha);
					graphics.drawRect(0,0,w,h);
					graphics.endFill();
					
					if(getStyle('backgroundImage')){
						var backgroundImage:Class=getStyle('backgroundImage');
						var bm:Bitmap = new backgroundImage;
						graphics.beginBitmapFill(bm.bitmapData,new Matrix(1,0,0,1,getStyle('backgroundPositionLeft'),getStyle('backgroundPositionTop')),getStyle('backgroundRepeat'));
						graphics.drawRect(0,0,bm.width,bm.height);
						graphics.endFill();
					}
				}
				
				if(getStyle('borderVisible')!=false){
					var cornerRadius:uint=getStyle('cornerRadius');

					var isComplex:Boolean=getStyle('topLeftRadius') || getStyle('topRightRadius') || getStyle('bottomLeftRadius') || getStyle('bottomRightRadius');

					var topLeftRadius:uint=isComplex?getStyle('topLeftRadius'):cornerRadius;
					var topRightRadius:uint=isComplex?getStyle('topRightRadius'):cornerRadius;
					var bottomLeftRadius:uint=isComplex?getStyle('bottomLeftRadius'):cornerRadius;
					var bottomRightRadius:uint=isComplex?getStyle('bottomRightRadius'):cornerRadius;

					var gradientBoxMatrix:Matrix = new Matrix();
					gradientBoxMatrix.createGradientBox(w, 30, Math.PI/2, 0, 0);  
					graphics.lineStyle(getStyle('borderThickness')*2,getStyle('borderColor'),getStyle('borderAlpha'));
					graphics.lineGradientStyle(GradientType.LINEAR, [0xd2d2d2,
						0x888888], [1, 1], [0, 255], gradientBoxMatrix);
					//graphics.lineGradientStyle(GradientType.LINEAR, [0x000000, 0xFFFFFF], [1, 1], [0, 255], verticalGradientMatrix(0, 0, w, h));
					graphics.drawRoundRectComplex(0,0,w,h,topLeftRadius,topRightRadius,bottomLeftRadius,bottomRightRadius);
					graphics.endFill();

					if(cornerRadius || isComplex){
						var _mask:Sprite=new Sprite;
						_mask.graphics.beginFill(0x0);
						_mask.graphics.drawRoundRectComplex(0, 0,w,h,topLeftRadius,topRightRadius,bottomLeftRadius,bottomRightRadius);
						_mask.graphics.endFill();
						mask = _mask;
						if(sysTitleBar){
							var _mask2:Sprite=new Sprite;
							_mask2.graphics.beginFill(0x0);
							_mask2.graphics.drawRoundRectComplex(getStyle('borderThickness'), getStyle('borderThickness'),w-getStyle('borderThickness')*2,h-getStyle('borderThickness')*2,topLeftRadius,topRightRadius,bottomLeftRadius,bottomRightRadius);
							_mask2.graphics.endFill();
							sysTitleBar.mask=_mask2;
						}
					}
				
				}
			}
			override protected function createBorder():void{
			}

			private var _moveObject:UIComponent;
			public function get moveObject():UIComponent{
				return _moveObject;
			}
			public function set moveObject(e:UIComponent):void{
				if(!e) {
					return;
				}
				_moveObject=e;
				_moveObject.addEventListener(MouseEvent.MOUSE_DOWN,startMove);
				_moveObject.addEventListener(MouseEvent.MOUSE_OVER,endMove);
				_moveObject.addEventListener(MouseEvent.MOUSE_UP,endMove);
			}
			
			private var startPoint:Point;

			public function startMove(e:MouseEvent):void{
				if(e.target!=_moveObject){
					return;
				}
				var rect:Rectangle=nativeWindow.bounds;
				startPoint=new Point(e.stageX,e.stageY);;
				addEventListener(MouseEvent.MOUSE_MOVE,moveHandler);
				Mouse.hide();
				Mouse.cursor=MouseCursor.HAND;
				Mouse.show();
			}

			private function moveHandler(e:MouseEvent):void{
				var rect:Rectangle=nativeWindow.bounds;
				rect.x+=e.stageX-startPoint.x;
				rect.y+=e.stageY-startPoint.y;
				nativeWindow.bounds=rect;
			}

			public function endMove(e:MouseEvent):void{
				if(e.target!=_moveObject){
					return;
				}
				removeEventListener(MouseEvent.MOUSE_MOVE,moveHandler);
				Mouse.hide();
				Mouse.cursor=MouseCursor.AUTO;
				Mouse.show();
			}
		]]>
	</fx:Script>
</mx:Window>
